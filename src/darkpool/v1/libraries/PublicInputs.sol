// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.24;

import { BN254 } from "solidity-bn254/BN254.sol";
import { BN254Helpers } from "renegade-lib/verifier/BN254Helpers.sol";
import { ExternalTransfer } from "darkpoolv1-types/Transfers.sol";
import { PublicRootKey } from "darkpoolv1-types/Keychain.sol";
import { OrderSettlementIndices, ExternalMatchResult, BoundedMatchResult } from "darkpoolv1-types/Settlement.sol";
import { FeeTake, FeeTakeRate } from "darkpoolv1-types/Fees.sol";
import { ElGamalCiphertext, EncryptionKey } from "renegade-lib/Ciphertext.sol";

// This file represents the public inputs (statements) for various proofs used by the darkpool

// -------------------
// | Statement Types |
// -------------------

/// @title ValidWalletCreateStatement
/// @notice The statement type for the `VALID WALLET CREATE` proof
struct ValidWalletCreateStatement {
    /// @dev The commitment to the wallet's secret shares
    BN254.ScalarField walletShareCommitment;
    /// @dev The public wallet shares of the wallet
    BN254.ScalarField[] publicShares;
}

/// @title ValidWalletUpdateStatement
/// @notice The statement type for the `VALID WALLET UPDATE` proof
struct ValidWalletUpdateStatement {
    /// @dev The nullifier of the previous wallet
    BN254.ScalarField previousNullifier;
    /// @dev A commitment to the new wallet's secret shares
    BN254.ScalarField newWalletCommitment;
    /// @dev The new public shares of the wallet
    BN254.ScalarField[] newPublicShares;
    /// @dev The global Merkle root that the old wallet shares open into
    BN254.ScalarField merkleRoot;
    /// @dev The external transfer in the update, zeroed out if there is no transfer
    ExternalTransfer externalTransfer;
    /// @dev The old public root key of the keychain
    PublicRootKey oldPkRoot;
}

/// @title ValidReblindStatement
/// @notice The statement type for the `VALID REBLIND` proof
struct ValidReblindStatement {
    /// @dev The nullifier of the original wallet
    BN254.ScalarField originalSharesNullifier;
    /// @dev A commitment to the new private shares of the reblinded wallet
    BN254.ScalarField newPrivateShareCommitment;
    /// @dev The global Merkle root that the new wallet shares open into
    BN254.ScalarField merkleRoot;
}

/// @title ValidCommitmentsStatement
/// @notice The statement type for the `VALID COMMITMENTS` proof
struct ValidCommitmentsStatement {
    /// @dev The order settlement indices of the party for which this statement is generated
    OrderSettlementIndices indices;
}

/// @title ValidMatchSettleStatement
/// @notice The statement type for the `VALID MATCH SETTLE` proof
struct ValidMatchSettleStatement {
    /// @dev The modified public shares of the first party
    BN254.ScalarField[] firstPartyPublicShares;
    /// @dev The modified public shares of the second party
    BN254.ScalarField[] secondPartyPublicShares;
    /// @dev The settlement indices of the first party
    OrderSettlementIndices firstPartySettlementIndices;
    /// @dev The settlement indices of the second party
    OrderSettlementIndices secondPartySettlementIndices;
    /// @dev The protocol fee rate used for the match
    /// @dev Note that this is a fixed point value encoded as a uint256
    /// @dev so the true fee rate is `protocolFeeRate / 2^{FIXED_POINT_PRECISION}`
    /// @dev Currently, the fixed point precision is 63
    uint256 protocolFeeRate;
}

/// @title ValidMatchSettleWithCommitmentsStatement
/// @notice The statement type for the `VALID MATCH SETTLE WITH COMMITMENTS` proof
struct ValidMatchSettleWithCommitmentsStatement {
    /// @dev The commitment to the first party's new wallet's private shares
    /// @dev This is placed in the statement to allow a verifier to check this value
    /// @dev against the value generated by a proof of `VALID REBLIND`
    BN254.ScalarField privateShareCommitment0;
    /// @dev The commitment to the second party's new wallet's private shares
    /// @dev This is placed in the statement to allow a verifier to check this value
    /// @dev against the value generated by a proof of `VALID REBLIND`
    BN254.ScalarField privateShareCommitment1;
    /// @dev The commitment to the first party's new wallet shares
    BN254.ScalarField newShareCommitment0;
    /// @dev The commitment to the second party's new wallet shares
    BN254.ScalarField newShareCommitment1;
    /// @dev The modified public shares of the first party
    BN254.ScalarField[] firstPartyPublicShares;
    /// @dev The modified public shares of the second party
    BN254.ScalarField[] secondPartyPublicShares;
    /// @dev The settlement indices of the first party
    OrderSettlementIndices firstPartySettlementIndices;
    /// @dev The settlement indices of the second party
    OrderSettlementIndices secondPartySettlementIndices;
    /// @dev The protocol fee rate used for the match
    /// @dev Note that this is a fixed point value encoded as a uint256
    /// @dev so the true fee rate is `protocolFeeRate / 2^{FIXED_POINT_PRECISION}`
    uint256 protocolFeeRate;
}

/// @title ValidMatchSettleAtomicStatement
/// @notice The statement type for the `VALID MATCH SETTLE ATOMIC` proof
struct ValidMatchSettleAtomicStatement {
    /// @dev The result of the match
    ExternalMatchResult matchResult;
    /// @dev The fees due by the external party
    FeeTake externalPartyFees;
    /// @dev The modified public shares of the internal party
    BN254.ScalarField[] internalPartyModifiedShares;
    /// @dev The order settlement indices of the internal party
    OrderSettlementIndices internalPartySettlementIndices;
    /// @dev The protocol fee rate used for the match
    /// @dev This is a fixed point value encoded as a uint256,
    /// @dev see `protocolFeeRate` in `ValidMatchSettleStatement`
    uint256 protocolFeeRate;
    /// @dev The address at which the relayer wishes to receive their fee due
    /// @dev from the external party
    address relayerFeeAddress;
}

/// @title ValidMatchSettleAtomicWithCommitmentsStatement
/// @notice The statement type for the `VALID MATCH SETTLE ATOMIC WITH COMMITMENTS` proof
struct ValidMatchSettleAtomicWithCommitmentsStatement {
    /// @dev A commitment to the internal party's private shares
    BN254.ScalarField privateShareCommitment;
    /// @dev A commitment to the new wallet shares of the internal party
    BN254.ScalarField newShareCommitment;
    /// @dev The result of the match
    ExternalMatchResult matchResult;
    /// @dev The external party's fee obligations as a result of the match
    FeeTake externalPartyFees;
    /// @dev The modified public shares of the internal party
    BN254.ScalarField[] internalPartyModifiedShares;
    /// @dev The indices that settlement should modify in the internal party's wallet
    OrderSettlementIndices internalPartySettlementIndices;
    /// @dev The protocol fee rate used for the match
    /// @dev This is a fixed point value encoded as a uint256,
    /// @dev so the true fee rate is `protocolFeeRate / 2^{FIXED_POINT_PRECISION}`
    uint256 protocolFeeRate;
    /// @dev The address at which the relayer wishes to receive their fee due
    /// @dev from the external party
    address relayerFeeAddress;
}

/// @title ValidMalleableMatchSettleAtomicStatement
/// @notice The statement type for the `VALID MALLEABLE MATCH SETTLE ATOMIC` proof
struct ValidMalleableMatchSettleAtomicStatement {
    /// @dev The result of the match
    BoundedMatchResult matchResult;
    /// @dev The fee rates charged to the external party
    FeeTakeRate externalFeeRates;
    /// @dev The fee rates charged to the internal party
    FeeTakeRate internalFeeRates;
    /// @dev The public wallet shares of the internal party
    BN254.ScalarField[] internalPartyPublicShares;
    /// @dev The address at which the relayer wishes to receive their fee due
    address relayerFeeAddress;
}

/// @title ValidOfflineFeeSettlementStatement
/// @notice The statement type for the `VALID OFFLINE FEE SETTLEMENT` proof
struct ValidOfflineFeeSettlementStatement {
    /// @dev The Merkle root to which inclusion is proven
    BN254.ScalarField merkleRoot;
    /// @dev The nullifier of the wallet paying the fee
    BN254.ScalarField walletNullifier;
    /// @dev The commitment to the payer's updated wallet shares
    BN254.ScalarField newWalletCommitment;
    /// @dev The public shares of the payer's updated wallet
    BN254.ScalarField[] updatedWalletPublicShares;
    /// @dev The ciphertext of the note
    ElGamalCiphertext noteCiphertext;
    /// @dev A commitment to the note
    BN254.ScalarField noteCommitment;
    /// @dev The encryption key of the protocol
    EncryptionKey protocolKey;
    /// @dev Whether or not the fee is paid to the protocol
    bool isProtocolFee;
}

/// @title ValidFeeRedemptionStatement
/// @notice The statement type for the `VALID FEE REDEMPTION` proof
struct ValidFeeRedemptionStatement {
    /// @dev The Merkle root to which the wallet inclusion is proven
    BN254.ScalarField walletRoot;
    /// @dev The Merkle root to which the note inclusion is proven
    BN254.ScalarField noteRoot;
    /// @dev The nullifier of the wallet
    BN254.ScalarField walletNullifier;
    /// @dev The nullifier of the note
    BN254.ScalarField noteNullifier;
    /// @dev A commitment to the new wallet's secret shares
    BN254.ScalarField newSharesCommitment;
    /// @dev The new public shares of the wallet
    BN254.ScalarField[] newWalletPublicShares;
    /// @dev The root key for the keychain of the wallet that redeems the note
    PublicRootKey walletRootKey;
}

// ------------------------
// | Scalar Serialization |
// ------------------------

/// @title StatementSerializer
/// @author Renegade Eng
/// @notice Library for serializing statement types to scalar arrays
library StatementSerializer {
    using StatementSerializer for ValidWalletCreateStatement;
    using StatementSerializer for ValidWalletUpdateStatement;
    using StatementSerializer for ValidReblindStatement;
    using StatementSerializer for ValidCommitmentsStatement;
    using StatementSerializer for ValidMatchSettleStatement;
    using StatementSerializer for ValidMatchSettleWithCommitmentsStatement;
    using StatementSerializer for ValidMatchSettleAtomicStatement;
    using StatementSerializer for ValidMatchSettleAtomicWithCommitmentsStatement;
    using StatementSerializer for ValidMalleableMatchSettleAtomicStatement;
    using StatementSerializer for ValidOfflineFeeSettlementStatement;
    using StatementSerializer for ValidFeeRedemptionStatement;
    using StatementSerializer for ExternalTransfer;
    using StatementSerializer for PublicRootKey;
    using StatementSerializer for OrderSettlementIndices;
    using StatementSerializer for ExternalMatchResult;
    using StatementSerializer for BoundedMatchResult;
    using StatementSerializer for FeeTake;
    using StatementSerializer for FeeTakeRate;
    using StatementSerializer for ElGamalCiphertext;
    using StatementSerializer for EncryptionKey;

    /// @notice The number of scalar field elements in a ValidWalletCreateStatement
    uint256 internal constant VALID_WALLET_CREATE_SCALAR_SIZE = 71;
    /// @notice The number of scalar field elements in a ValidWalletUpdateStatement
    uint256 internal constant VALID_WALLET_UPDATE_SCALAR_SIZE = 81;
    /// @notice The number of scalar field elements in a ValidReblindStatement
    uint256 internal constant VALID_REBLIND_SCALAR_SIZE = 3;
    /// @notice The number of scalar field elements in a ValidCommitmentsStatement
    uint256 internal constant VALID_COMMITMENTS_SCALAR_SIZE = 3;
    /// @notice The number of scalar field elements in a ValidMatchSettleStatement
    uint256 internal constant VALID_MATCH_SETTLE_SCALAR_SIZE = 147;
    /// @notice The number of scalar field elements in a ValidMatchSettleWithCommitmentsStatement
    uint256 internal constant VALID_MATCH_SETTLE_WITH_COMMITMENTS_SCALAR_SIZE = 151;
    /// @notice The number of scalar field elements in a ValidMatchSettleAtomicStatement
    uint256 internal constant VALID_MATCH_SETTLE_ATOMIC_SCALAR_SIZE = 82;
    /// @notice The number of scalar field elements in a ValidMatchSettleAtomicWithCommitmentsStatement
    uint256 internal constant VALID_MATCH_SETTLE_ATOMIC_WITH_COMMITMENTS_SCALAR_SIZE = 84;
    /// @notice The number of scalar field elements in a ValidMalleableMatchSettleAtomicStatement
    uint256 internal constant VALID_MALLEABLE_MATCH_SETTLE_ATOMIC_SCALAR_SIZE = 81;
    /// @notice The number of scalar field elements in a ValidOfflineFeeSettlementStatement
    uint256 internal constant VALID_OFFLINE_FEE_SETTLEMENT_SCALAR_SIZE = 82;
    /// @notice The number of scalar field elements in a ValidFeeRedemptionStatement
    uint256 internal constant VALID_FEE_REDEMPTION_SCALAR_SIZE = 79;

    // --- Valid Wallet Create --- //

    /// @notice Serializes a ValidWalletCreateStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidWalletCreateStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        // Create array with size = 1 (for privateShareCommitment) + publicShares.length
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_WALLET_CREATE_SCALAR_SIZE);

        // Add the wallet commitment
        serialized[0] = self.walletShareCommitment;

        // Add all public shares
        for (uint256 i = 0; i < self.publicShares.length; ++i) {
            serialized[i + 1] = self.publicShares[i];
        }

        return serialized;
    }

    // --- Valid Wallet Update --- //

    /// @notice Serializes a ValidWalletUpdateStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidWalletUpdateStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_WALLET_UPDATE_SCALAR_SIZE);
        serialized[0] = self.previousNullifier;
        serialized[1] = self.newWalletCommitment;

        // Copy the public shares
        uint256 n = self.newPublicShares.length;
        for (uint256 i = 0; i < n; ++i) {
            serialized[i + 2] = self.newPublicShares[i];
        }

        serialized[n + 2] = self.merkleRoot;
        BN254.ScalarField[] memory externalTransferSerialized = self.externalTransfer.scalarSerialize();
        for (uint256 i = 0; i < externalTransferSerialized.length; ++i) {
            serialized[n + 3 + i] = externalTransferSerialized[i];
        }

        BN254.ScalarField[] memory oldPkRootSerialized = self.oldPkRoot.scalarSerialize();
        for (uint256 i = 0; i < oldPkRootSerialized.length; ++i) {
            serialized[n + 3 + externalTransferSerialized.length + i] = oldPkRootSerialized[i];
        }

        return serialized;
    }

    // --- Valid Reblind --- //

    /// @notice Serializes a ValidReblindStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidReblindStatement memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_REBLIND_SCALAR_SIZE);
        serialized[0] = self.originalSharesNullifier;
        serialized[1] = self.newPrivateShareCommitment;
        serialized[2] = self.merkleRoot;

        return serialized;
    }

    // --- Valid Commitments --- //

    /// @notice Serializes a ValidCommitmentsStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidCommitmentsStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_COMMITMENTS_SCALAR_SIZE);
        serialized[0] = BN254.ScalarField.wrap(self.indices.balanceSend);
        serialized[1] = BN254.ScalarField.wrap(self.indices.balanceReceive);
        serialized[2] = BN254.ScalarField.wrap(self.indices.order);

        return serialized;
    }

    // --- Valid Match Settle --- //

    /// @notice Serializes a ValidMatchSettleStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidMatchSettleStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_MATCH_SETTLE_SCALAR_SIZE);

        // Copy the public shares
        uint256 n = self.firstPartyPublicShares.length;
        for (uint256 i = 0; i < n; ++i) {
            serialized[i] = self.firstPartyPublicShares[i];
        }

        // Copy the second party public shares
        uint256 offset = self.firstPartyPublicShares.length;
        for (uint256 i = 0; i < n; ++i) {
            serialized[offset + i] = self.secondPartyPublicShares[i];
        }

        // Copy the settlement indices
        offset += n;
        BN254.ScalarField[] memory firstPartySettlementIndicesSerialized =
            self.firstPartySettlementIndices.scalarSerialize();
        for (uint256 i = 0; i < firstPartySettlementIndicesSerialized.length; ++i) {
            serialized[offset + i] = firstPartySettlementIndicesSerialized[i];
        }

        // Copy the second party settlement indices
        offset += firstPartySettlementIndicesSerialized.length;
        BN254.ScalarField[] memory secondPartySettlementIndicesSerialized =
            self.secondPartySettlementIndices.scalarSerialize();
        for (uint256 i = 0; i < secondPartySettlementIndicesSerialized.length; ++i) {
            serialized[offset + i] = secondPartySettlementIndicesSerialized[i];
        }

        // Copy the protocol fee rate
        serialized[serialized.length - 1] = BN254.ScalarField.wrap(self.protocolFeeRate);
        return serialized;
    }

    // --- Valid Match Settle With Commitments --- //

    /// @notice Serializes a ValidMatchSettleWithCommitmentsStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidMatchSettleWithCommitmentsStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_MATCH_SETTLE_WITH_COMMITMENTS_SCALAR_SIZE);

        // Copy the commitments
        serialized[0] = self.privateShareCommitment0;
        serialized[1] = self.privateShareCommitment1;
        serialized[2] = self.newShareCommitment0;
        serialized[3] = self.newShareCommitment1;

        // Copy the public shares
        uint256 offset = 4;
        uint256 n = self.firstPartyPublicShares.length;
        for (uint256 i = 0; i < n; ++i) {
            serialized[offset + i] = self.firstPartyPublicShares[i];
        }

        // Copy the second party public shares
        offset += n;
        for (uint256 i = 0; i < n; ++i) {
            serialized[offset + i] = self.secondPartyPublicShares[i];
        }

        // Copy the settlement indices
        offset += n;
        BN254.ScalarField[] memory firstPartySettlementIndicesSerialized =
            self.firstPartySettlementIndices.scalarSerialize();
        for (uint256 i = 0; i < firstPartySettlementIndicesSerialized.length; ++i) {
            serialized[offset + i] = firstPartySettlementIndicesSerialized[i];
        }

        // Copy the second party settlement indices
        offset += firstPartySettlementIndicesSerialized.length;
        BN254.ScalarField[] memory secondPartySettlementIndicesSerialized =
            self.secondPartySettlementIndices.scalarSerialize();
        for (uint256 i = 0; i < secondPartySettlementIndicesSerialized.length; ++i) {
            serialized[offset + i] = secondPartySettlementIndicesSerialized[i];
        }

        // Copy the protocol fee rate
        serialized[serialized.length - 1] = BN254.ScalarField.wrap(self.protocolFeeRate);
        return serialized;
    }

    // --- Valid Match Settle Atomic --- //

    /// @notice Serializes a ValidMatchSettleAtomicStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidMatchSettleAtomicStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_MATCH_SETTLE_ATOMIC_SCALAR_SIZE);

        // Copy the match result
        BN254.ScalarField[] memory matchResultSerialized = self.matchResult.scalarSerialize();
        for (uint256 i = 0; i < matchResultSerialized.length; ++i) {
            serialized[i] = matchResultSerialized[i];
        }

        // Copy the external party fees
        uint256 offset = matchResultSerialized.length;
        BN254.ScalarField[] memory externalPartyFeesSerialized = self.externalPartyFees.scalarSerialize();
        for (uint256 i = 0; i < externalPartyFeesSerialized.length; ++i) {
            serialized[offset + i] = externalPartyFeesSerialized[i];
        }

        // Copy the internal party modified shares
        offset += externalPartyFeesSerialized.length;
        for (uint256 i = 0; i < self.internalPartyModifiedShares.length; ++i) {
            serialized[offset + i] = self.internalPartyModifiedShares[i];
        }

        // Copy the internal party settlement indices
        offset += self.internalPartyModifiedShares.length;
        BN254.ScalarField[] memory internalPartySettlementIndicesSerialized =
            self.internalPartySettlementIndices.scalarSerialize();
        for (uint256 i = 0; i < internalPartySettlementIndicesSerialized.length; ++i) {
            serialized[offset + i] = internalPartySettlementIndicesSerialized[i];
        }

        // Copy the protocol fee rate and relayer fee address
        serialized[serialized.length - 2] = BN254.ScalarField.wrap(self.protocolFeeRate);
        serialized[serialized.length - 1] = BN254.ScalarField.wrap(uint256(uint160(self.relayerFeeAddress)));
        return serialized;
    }

    // --- Valid Match Settle Atomic With Commitments --- //

    /// @notice Serializes a ValidMatchSettleAtomicWithCommitmentsStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidMatchSettleAtomicWithCommitmentsStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized =
            new BN254.ScalarField[](VALID_MATCH_SETTLE_ATOMIC_WITH_COMMITMENTS_SCALAR_SIZE);

        // Copy the private share commitment
        serialized[0] = self.privateShareCommitment;
        serialized[1] = self.newShareCommitment;

        // Copy the match result
        uint256 offset = 2;
        BN254.ScalarField[] memory matchResultSerialized = self.matchResult.scalarSerialize();
        for (uint256 i = 0; i < matchResultSerialized.length; ++i) {
            serialized[offset + i] = matchResultSerialized[i];
        }

        // Copy the external party fees
        offset += matchResultSerialized.length;
        BN254.ScalarField[] memory externalPartyFeesSerialized = self.externalPartyFees.scalarSerialize();
        for (uint256 i = 0; i < externalPartyFeesSerialized.length; ++i) {
            serialized[offset + i] = externalPartyFeesSerialized[i];
        }

        // Copy the internal party modified shares
        offset += externalPartyFeesSerialized.length;
        for (uint256 i = 0; i < self.internalPartyModifiedShares.length; ++i) {
            serialized[offset + i] = self.internalPartyModifiedShares[i];
        }

        // Copy the internal party settlement indices
        offset += self.internalPartyModifiedShares.length;
        BN254.ScalarField[] memory internalPartySettlementIndicesSerialized =
            self.internalPartySettlementIndices.scalarSerialize();
        for (uint256 i = 0; i < internalPartySettlementIndicesSerialized.length; ++i) {
            serialized[offset + i] = internalPartySettlementIndicesSerialized[i];
        }

        // Copy the protocol fee rate and relayer fee address
        serialized[serialized.length - 2] = BN254.ScalarField.wrap(self.protocolFeeRate);
        serialized[serialized.length - 1] = BN254.ScalarField.wrap(uint256(uint160(self.relayerFeeAddress)));
        return serialized;
    }

    // --- Valid Malleable Match Settle Atomic --- //

    /// @notice Serializes a ValidMalleableMatchSettleAtomicStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidMalleableMatchSettleAtomicStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_MALLEABLE_MATCH_SETTLE_ATOMIC_SCALAR_SIZE);

        // Copy the match result
        BN254.ScalarField[] memory matchResultSerialized = self.matchResult.scalarSerialize();
        for (uint256 i = 0; i < matchResultSerialized.length; ++i) {
            serialized[i] = matchResultSerialized[i];
        }

        // Copy the external fee rates
        uint256 offset = matchResultSerialized.length;
        BN254.ScalarField[] memory externalFeeRatesSerialized = self.externalFeeRates.scalarSerialize();
        for (uint256 i = 0; i < externalFeeRatesSerialized.length; ++i) {
            serialized[offset + i] = externalFeeRatesSerialized[i];
        }

        // Copy the internal fee rates
        offset += externalFeeRatesSerialized.length;
        BN254.ScalarField[] memory internalFeeRatesSerialized = self.internalFeeRates.scalarSerialize();
        for (uint256 i = 0; i < internalFeeRatesSerialized.length; ++i) {
            serialized[offset + i] = internalFeeRatesSerialized[i];
        }

        // Copy the internal party public shares
        offset += internalFeeRatesSerialized.length;
        for (uint256 i = 0; i < self.internalPartyPublicShares.length; ++i) {
            serialized[offset + i] = self.internalPartyPublicShares[i];
        }

        // Copy the relayer fee address
        serialized[serialized.length - 1] = BN254.ScalarField.wrap(uint256(uint160(self.relayerFeeAddress)));
        return serialized;
    }

    // --- Valid Offline Fee Settlement --- //

    /// @notice Serializes a ValidOfflineFeeSettlementStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidOfflineFeeSettlementStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_OFFLINE_FEE_SETTLEMENT_SCALAR_SIZE);
        serialized[0] = self.merkleRoot;
        serialized[1] = self.walletNullifier;
        serialized[2] = self.newWalletCommitment;

        // Serialize the updated wallet public shares
        uint256 offset = 3;
        for (uint256 i = 0; i < self.updatedWalletPublicShares.length; ++i) {
            serialized[offset + i] = self.updatedWalletPublicShares[i];
        }
        offset += self.updatedWalletPublicShares.length;

        // Serialize the note ciphertext
        BN254.ScalarField[] memory noteCiphertextSerialized = self.noteCiphertext.scalarSerialize();
        for (uint256 i = 0; i < noteCiphertextSerialized.length; ++i) {
            serialized[offset + i] = noteCiphertextSerialized[i];
        }
        offset += noteCiphertextSerialized.length;

        // Serialize the note commitment
        serialized[offset] = self.noteCommitment;
        ++offset;

        // Serialize the protocol key
        BN254.ScalarField[] memory protocolKeySerialized = self.protocolKey.scalarSerialize();
        for (uint256 i = 0; i < protocolKeySerialized.length; ++i) {
            serialized[offset + i] = protocolKeySerialized[i];
        }
        offset += protocolKeySerialized.length;

        // Serialize the is protocol fee flag
        serialized[offset] = self.isProtocolFee ? BN254Helpers.ONE : BN254Helpers.ZERO;
        return serialized;
    }

    // --- Valid Fee Redemption --- //

    /// @notice Serializes a ValidFeeRedemptionStatement into an array of scalar field elements
    /// @param self The statement to serialize
    /// @return serialized The serialized statement as an array of scalar field elements
    function scalarSerialize(ValidFeeRedemptionStatement memory self)
        internal
        pure
        returns (BN254.ScalarField[] memory)
    {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](VALID_FEE_REDEMPTION_SCALAR_SIZE);
        serialized[0] = self.walletRoot;
        serialized[1] = self.noteRoot;
        serialized[2] = self.walletNullifier;
        serialized[3] = self.noteNullifier;
        serialized[4] = self.newSharesCommitment;

        // Serialize the new wallet public shares
        uint256 offset = 5;
        for (uint256 i = 0; i < self.newWalletPublicShares.length; ++i) {
            serialized[offset + i] = self.newWalletPublicShares[i];
        }
        offset += self.newWalletPublicShares.length;

        // Serialize the wallet root key
        BN254.ScalarField[] memory walletRootKeySerialized = self.walletRootKey.scalarSerialize();
        for (uint256 i = 0; i < walletRootKeySerialized.length; ++i) {
            serialized[offset + i] = walletRootKeySerialized[i];
        }
        return serialized;
    }

    // --- Types --- //

    /// @notice Serializes an ExternalTransfer into an array of scalar field elements
    /// @param self The transfer to serialize
    /// @return serialized The serialized transfer as an array of scalar field elements
    function scalarSerialize(ExternalTransfer memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](4);

        serialized[0] = BN254.ScalarField.wrap(uint256(uint160(self.account)));
        serialized[1] = BN254.ScalarField.wrap(uint256(uint160(self.mint)));
        serialized[2] = BN254.ScalarField.wrap(self.amount);
        serialized[3] = BN254.ScalarField.wrap(uint256(self.transferType));

        return serialized;
    }

    /// @notice Serializes a PublicRootKey into an array of scalar field elements
    /// @param self The key to serialize
    /// @return serialized The serialized key as an array of scalar field elements
    function scalarSerialize(PublicRootKey memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](4);

        serialized[0] = self.x[0];
        serialized[1] = self.x[1];
        serialized[2] = self.y[0];
        serialized[3] = self.y[1];

        return serialized;
    }

    /// @notice Serializes an OrderSettlementIndices into an array of scalar field elements
    /// @param self The indices to serialize
    /// @return serialized The serialized indices as an array of scalar field elements
    function scalarSerialize(OrderSettlementIndices memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](3);

        serialized[0] = BN254.ScalarField.wrap(self.balanceSend);
        serialized[1] = BN254.ScalarField.wrap(self.balanceReceive);
        serialized[2] = BN254.ScalarField.wrap(self.order);

        return serialized;
    }

    /// @notice Serializes an ExternalMatchResult into an array of scalar field elements
    /// @param self The result to serialize
    /// @return serialized The serialized result as an array of scalar field elements
    function scalarSerialize(ExternalMatchResult memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](5);
        serialized[0] = BN254.ScalarField.wrap(uint256(uint160(self.quoteMint)));
        serialized[1] = BN254.ScalarField.wrap(uint256(uint160(self.baseMint)));
        serialized[2] = BN254.ScalarField.wrap(self.quoteAmount);
        serialized[3] = BN254.ScalarField.wrap(self.baseAmount);
        serialized[4] = BN254.ScalarField.wrap(uint256(self.direction));

        return serialized;
    }

    /// @notice Serializes a BoundedMatchResult into an array of scalar field elements
    /// @param self The result to serialize
    /// @return serialized The serialized result as an array of scalar field elements
    function scalarSerialize(BoundedMatchResult memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](6);

        serialized[0] = BN254.ScalarField.wrap(uint256(uint160(self.quoteMint)));
        serialized[1] = BN254.ScalarField.wrap(uint256(uint160(self.baseMint)));
        serialized[2] = BN254.ScalarField.wrap(self.price.repr);
        serialized[3] = BN254.ScalarField.wrap(self.minBaseAmount);
        serialized[4] = BN254.ScalarField.wrap(self.maxBaseAmount);
        serialized[5] = BN254.ScalarField.wrap(uint256(self.direction));
        return serialized;
    }

    /// @notice Serializes a FeeTake into an array of scalar field elements
    /// @param self The fee take to serialize
    /// @return serialized The serialized fee take as an array of scalar field elements
    function scalarSerialize(FeeTake memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](2);
        serialized[0] = BN254.ScalarField.wrap(self.relayerFee);
        serialized[1] = BN254.ScalarField.wrap(self.protocolFee);

        return serialized;
    }

    /// @notice Serializes a FeeTakeRate into an array of scalar field elements
    /// @param self The fee take rate to serialize
    /// @return serialized The serialized fee take rate as an array of scalar field elements
    function scalarSerialize(FeeTakeRate memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](2);
        serialized[0] = BN254.ScalarField.wrap(self.relayerFeeRate.repr);
        serialized[1] = BN254.ScalarField.wrap(self.protocolFeeRate.repr);

        return serialized;
    }

    /// @notice Serializes an ElGamalCiphertext into an array of scalar field elements
    /// @param self The ciphertext to serialize
    /// @return serialized The serialized ciphertext as an array of scalar field elements
    function scalarSerialize(ElGamalCiphertext memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](2 + self.ciphertext.length);
        serialized[0] = self.ephemeralKey.x;
        serialized[1] = self.ephemeralKey.y;

        for (uint256 i = 0; i < self.ciphertext.length; ++i) {
            serialized[2 + i] = self.ciphertext[i];
        }
        return serialized;
    }

    /// @notice Serializes an EncryptionKey into an array of scalar field elements
    /// @param self The key to serialize
    /// @return serialized The serialized key as an array of scalar field elements
    function scalarSerialize(EncryptionKey memory self) internal pure returns (BN254.ScalarField[] memory) {
        BN254.ScalarField[] memory serialized = new BN254.ScalarField[](2);
        serialized[0] = self.point.x;
        serialized[1] = self.point.y;

        return serialized;
    }
}
